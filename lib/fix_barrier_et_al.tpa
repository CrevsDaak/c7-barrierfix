

// fix_barrier_et_al.tpa
// Time-stamp: </Users/nico/BG_modding/c7-fixpack/lib/fix_barrier_et_al.tpa, 2024-06-29 Saturday 23:32:00 nico>

/*
 * This little mod does one thing: stop Blade Barrier from instagibbing.
 * 
 * Note that this ONLY occurs when Casting Fixes on the ToBEx ini is set to 0, which is necessary to run ToBEx on wine.
 *
 * What happens then is that this bug causes effects applied via op232 to reapply themselves infinitely
 * while the game is paused IF one pauses the game in the same frame that the spell would be applied in.
 *
 * I think I might be adding an extra proc of BB at 1s, but it should still be fine, as the spell is now finally balanced.
 *
 */


/*
      LPF ADD_SPELL_EFFECT
        INT_VAR
          opcode = 206
          target = 1
          resist_dispel = 3
          parameter1 = RESOLVE_STR_REF(@206)
          duration = 62
        STR_VAR
          resource = EVAL "%spell%"
      END
*/

// blade barrier
ACTION_FOR_EACH spell IN dwsp603 dwsp698 spin143 sppr603 sppr698 BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME ~%spell%.spl~ BEGIN
    COPY_EXISTING "%spell%.spl" override
      LPF DELETE_SPELL_EFFECT INT_VAR opcode_to_delete = 232 END
      FOR (i=0; i < 11; i +=1) BEGIN
        LPF ADD_SPELL_EFFECT
          INT_VAR
            opcode = 146
            target = 2
            parameter2 = 1
            timing = 3
            resist_dispel = 3
            duration = (i*6 + 1)
          STR_VAR
            resource = "sppr603d"
        END
      END
    BUT_ONLY
  END
END

// smellyssan blade barrier (uses different dmg spell)
ACTION_IF FILE_EXISTS_IN_GAME melis02.spl BEGIN
  COPY_EXISTING "%spell%.spl" override
    LPF DELETE_SPELL_EFFECT INT_VAR opcode_to_delete = 232 END
    FOR (i=0; i < 11; i +=1) BEGIN
      LPF ADD_SPELL_EFFECT
        INT_VAR
          opcode = 146
          target = 2
          parameter2 = 1
          timing = 3
          resist_dispel = 3
          duration = (i*6 + 1)
        STR_VAR
          resource = "melis02b"
      END
    END
  BUT_ONLY
END

// globe of blades
ACTION_FOR_EACH spell IN dwsc116 spcl116 sppr725 BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME ~%spell%.spl~ BEGIN
    COPY_EXISTING "%spell%.spl" override
      LPF DELETE_SPELL_EFFECT INT_VAR opcode_to_delete = 232 END
      FOR (i=0; i < 11; i +=1) BEGIN
        LPF ADD_SPELL_EFFECT
          INT_VAR
            opcode = 146
            target = 2
            parameter2 = 1
            timing = 3
            resist_dispel = 3
            duration = (i*6 + 1)
          STR_VAR
            resource = "sppr725d"
        END
      END
    BUT_ONLY
  END
END

// circle of bones. Honestly, f*ck this spell lol
ACTION_FOR_EACH spell IN dwsp324 sppr324 BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME ~%spell%.spl~ BEGIN
    COPY_EXISTING "%spell%.spl" override
      LPF DELETE_SPELL_EFFECT INT_VAR opcode_to_delete = 177 END
      LPF DELETE_SPELL_EFFECT INT_VAR opcode_to_delete = 206 END
      FOR (startn=1; startn<17; startn+=1) BEGIN // header loop
        FOR (i = startn; i<17; i+=1) BEGIN // min level limiting header loop
          FOR (j= startn < 2 ? startn - 1: startn + 3; j<startn+4; j+=1) BEGIN // duration loop
            LPF ADD_SPELL_EFFECT
              INT_VAR
                opcode = 146
                target = 2
                parameter2 = 1
                timing = 3
                resist_dispel = 2
                duration = (j*6)
                header = i
              STR_VAR
                resource = "sppr324d"
            END
          END
        END
      END
      // remove the extra effect added at lv1
      LPF DELETE_EFFECT
        INT_VAR
          match_opcode = 146
          match_duration = 24
          header = 0
      END
    BUT_ONLY
  END
END


/*
 * See: https://www.gibberlings3.net/forums/topic/38433-v3517-unexpected-behaviour-with-planetars-in-time-stop/?do=findComment&comment=339655
 *
 */
ACTION_FOR_EACH barrierd IN sppr324d sppr725d melis02b sppr603d BEGIN
  COPY_EXISTING "%barrierd%.spl" override
    PATCH_IF SOURCE_SIZE > 0x71 BEGIN
      LPF ADD_SPELL_EFFECT
        INT_VAR
          opcode = 206
          target = 2
          timing = 10
          duration = 2
          resist_dispel = 2
        STR_VAR
          resource = EVAL "%barrierd%"
      END
    END
  BUT_ONLY IF_EXISTS
END